#version 430 core

layout (local_size_x = 32, local_size_y = 1, local_size_z = 1) in;

struct InstanceData{
    mat4 matrix;
    float radius;
    uint modelIndex;
    uint _padding[2];
};

layout (std430, binding = 0) buffer InputBuffer {
    InstanceData instanceIn[];
};

layout (std430, binding = 1) buffer OutputBuffer {
    mat4 o[];
};

struct ModelInstanceData{
    uint count;
    uint offset;
};

layout (std430, binding = 2) buffer ModelInstanceDataBuffer {
    ModelInstanceData modelData[];
};

struct Plane {
    vec3 normal;
    float distance;
};

uniform Plane planes[6];

uniform int inputNumber = 0;

bool isVisible(vec3 pos, float radius);

void main() {
    uint index = gl_WorkGroupID.z * 32 + gl_LocalInvocationID.x;
    if(index < inputNumber) {
        InstanceData instance = instanceIn[index];
        vec3 pos = vec3(instance.matrix[3][0], instance.matrix[3][1], instance.matrix[3][2]);
        if (isVisible(pos, instance.radius)) {
            uint n = atomicAdd(modelData[instance.modelIndex].count, 1);
            o[modelData[instance.modelIndex].offset + n] = instance.matrix;
        }
    }
}

bool outsidePlane(vec3 center, Plane plane, float radius) {

    float centerOffset = dot(center, plane.normal) - plane.distance;

    // If the center at most range less than the plane along its normal the box intersects the plane
    return centerOffset + radius < 0;
}

bool isVisible(vec3 pos, float radius) {
    // loop through all planes
    for (int i = 0; i < 6; i++) {
        if (outsidePlane(pos, planes[i], radius))
        return false;
    }
    return true;
}